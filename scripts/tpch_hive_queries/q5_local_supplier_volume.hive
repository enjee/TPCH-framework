DROP TABLE q5_customer;
DROP TABLE q5_orders;
DROP TABLE q5_lineitem;
DROP TABLE q5_supplier;
DROP TABLE q5_nation;
DROP TABLE q5_region;
DROP TABLE q5_local_supplier_volume;

-- create tables and load data
CREATE EXTERNAL TABLE IF NOT EXISTS q5_customer (C_CUSTKEY INT, C_NAME STRING, C_ADDRESS STRING, C_NATIONKEY INT, C_PHONE STRING, C_ACCTBAL DOUBLE, C_MKTSEGMENT STRING, C_COMMENT STRING) ROW FORMAT DELIMITED FIELDS TERMINATED BY '|';
LOAD DATA INPATH 'wasb://size_placeholdergb@benchmarkdatasaxion.blob.core.windows.net/customer.tbl' INTO TABLE q5_customer;

CREATE EXTERNAL TABLE IF NOT EXISTS q5_lineitem (L_ORDERKEY INT, L_PARTKEY INT, L_SUPPKEY INT, L_LINENUMBER INT, L_QUANTITY DOUBLE, L_EXTENDEDPRICE DOUBLE, L_DISCOUNT DOUBLE, L_TAX DOUBLE, L_RETURNFLAG STRING, L_LINESTATUS STRING, L_SHIPDATE STRING, L_COMMITDATE STRING, L_RECEIPTDATE STRING, L_SHIPINSTRUCT STRING, L_SHIPMODE STRING, L_COMMENT STRING) ROW FORMAT DELIMITED FIELDS TERMINATED BY '|';
LOAD DATA INPATH 'wasb://size_placeholdergb@benchmarkdatasaxion.blob.core.windows.net/lineitem.tbl' INTO TABLE q5_lineitem;

CREATE EXTERNAL TABLE IF NOT EXISTS q5_orders (O_ORDERKEY INT, O_CUSTKEY INT, O_ORDERSTATUS STRING, O_TOTALPRICE DOUBLE, O_ORDERDATE STRING, O_ORDERPRIORITY STRING, O_CLERK STRING, O_SHIPPRIORITY INT, O_COMMENT STRING) ROW FORMAT DELIMITED FIELDS TERMINATED BY '|';
LOAD DATA INPATH 'wasb://size_placeholdergb@benchmarkdatasaxion.blob.core.windows.net/orders.tbl' INTO TABLE q5_orders;

CREATE EXTERNAL TABLE IF NOT EXISTS q5_supplier (S_SUPPKEY INT, S_NAME STRING, S_ADDRESS STRING, S_NATIONKEY INT, S_PHONE STRING, S_ACCTBAL DOUBLE, S_COMMENT STRING) ROW FORMAT DELIMITED FIELDS TERMINATED BY '|';
LOAD DATA INPATH 'wasb://size_placeholdergb@benchmarkdatasaxion.blob.core.windows.net/supplier.tbl' INTO TABLE q5_supplier;

CREATE EXTERNAL TABLE IF NOT EXISTS q5_nation (N_NATIONKEY INT, N_NAME STRING, N_REGIONKEY INT, N_COMMENT STRING) ROW FORMAT DELIMITED FIELDS TERMINATED BY '|';
LOAD DATA INPATH 'wasb://size_placeholdergb@benchmarkdatasaxion.blob.core.windows.net/nation.tbl' INTO TABLE q5_nation;

CREATE EXTERNAL TABLE IF NOT EXISTS q5_region (R_REGIONKEY INT, R_NAME STRING, R_COMMENT STRING) ROW FORMAT DELIMITED FIELDS TERMINATED BY '|';
LOAD DATA INPATH 'wasb://size_placeholdergb@benchmarkdatasaxion.blob.core.windows.net/region.tbl' INTO TABLE q5_region;

-- create the target table
create table q5_local_supplier_volume (N_NAME STRING, REVENUE DOUBLE);

set mapred.min.split.size=536870912;

-- the query
insert overwrite table q5_local_supplier_volume 
select 
  n_name, sum(l_extendedprice * (1 - l_discount)) as revenue 
from
  q5_customer c join
    ( select n_name, l_extendedprice, l_discount, s_nationkey, o_custkey from q5_orders o join
      ( select n_name, l_extendedprice, l_discount, l_orderkey, s_nationkey from q5_lineitem l join
        ( select n_name, s_suppkey, s_nationkey from q5_supplier s join
          ( select n_name, n_nationkey 
            from q5_nation n join q5_region r 
            on n.n_regionkey = r.r_regionkey and r.r_name = 'ASIA'
          ) n1 on s.s_nationkey = n1.n_nationkey
        ) s1 on l.l_suppkey = s1.s_suppkey
      ) l1 on l1.l_orderkey = o.o_orderkey and o.o_orderdate >= '1994-01-01' 
              and o.o_orderdate < '1995-01-01'
) o1 
on c.c_nationkey = o1.s_nationkey and c.c_custkey = o1.o_custkey
group by n_name 
order by revenue desc;

